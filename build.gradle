plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.1'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'com.google.cloud.tools.jib' version '3.4.4'
}

group = 'io.hardplastik'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

jib {
	from {
		image = 'eclipse-temurin:17.0.14_7-jre-alpine'
	}
	to {
		image = 'hardplastik/ds-api'
		tags = ['latest']
	}
	container {
		jvmFlags = [
			'-Xms512m', '-Xmx512m',
			'-XX:+UseContainerSupport',
			'-XX:MaxRAMPercentage=75.0',
			'-XX:+UseG1GC',
			'-XX:MaxGCPauseMillis=200',
			'-XX:+AlwaysPreTouch',
			'-XX:+ParallelRefProcEnabled',
			'-Djava.security.egd=file:/dev/urandom'
		]
		creationTime = 'USE_CURRENT_TIMESTAMP'
	}
}

tasks.register('build-local') {
	description = 'Builds the docker image locally'
	group = 'build'
	doLast {}
	dependsOn('jibDockerBuild');
}


dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.flywaydb:flyway-core'
	implementation 'org.flywaydb:flyway-database-postgresql'
	implementation 'io.jsonwebtoken:jjwt-api:0.12.6'
	implementation 'io.jsonwebtoken:jjwt-jackson:0.12.6'
	implementation 'io.jsonwebtoken:jjwt-impl:0.12.6'
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'org.postgresql:postgresql'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
}
